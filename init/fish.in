#
# Init file for fish
#


#
# Set default field separators
#

set -g IFS \ \t\n

#
# Add a few common directories to path, if they exists. Note that pure
# console programs like makedep sometimes live in /usr/X11R6/bin, so we
# want this even for text-only terminals.
#

set -l path_list /bin /usr/bin /usr/X11R6/bin @PREFIX@/bin

set -l uid (id -u 2>/dev/null)
if test "$uid" = 0
	set path_list $path_list /sbin /usr/sbin /usr/local/sbin
end

for i in $path_list
	if not expr "$PATH" : .\*$i.\* >/dev/null
		if test -d $i
			set PATH $PATH $i
		end
	end
end




#
# Set some value for LANG if nothing was set before
#

if status --is-login
	if not set -q LANG >/dev/null
		set -gx LANG en_US.UTF-8
	end
end


#
# Put linux console in unicode mode. Should this be done in any other
# situation as well?
#

if expr match "$LANG" ".*UTF" >/dev/null
	if test linux = "$TERM"
		unicode_start ^/dev/null
	end
end


#
# There are variables that contain colons that are not arrays. This 
# reverts them back to regular strings.
#

for i in DISPLAY
	if set -q $i
		set -- $i (printf ":%s" $$i|cut -c 2-)
	end
end

#
# Load additional initialization files
#

for i in fish.d/*.fish
	. $i
end
